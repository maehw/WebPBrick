// vending3.nqc
//  program for message based Vending Machine with data logging

#define CANDY_COUNT	SENSOR_3
#define CANDY_MOTOR	OUT_C

#define DATALOG_SIZE	100

task main()
{
	// setup sensors
	SetSensor(CANDY_COUNT, SENSOR_TOUCH);
	
	init_datalog();
	
	while(true)
	{
		// wait for message
		until(Message() != 0);
		
		// dispense candy
		dispenseCandy(Message());
		
		// get ready for next message
		ClearMessage();
	}
}

void init_datalog()
{
	// if not pressed, don't init datalog
	if (CANDY_COUNT == 0) return;
	
	CreateDatalog(0);
	CreateDatalog(DATALOG_SIZE);
	PlaySound(SOUND_FAST_UP);
	
	// wait for button to be released
	until(CANDY_COUNT==0);
}


void dispenseCandy(int n)
{
	// log the request
	AddToDatalog(n);

	// dispsense the candy
	On(CANDY_MOTOR);
	repeat (n)
	{
		until(CANDY_COUNT == 1);
		until(CANDY_COUNT == 0);
	}
	Off(CANDY_MOTOR);
}
