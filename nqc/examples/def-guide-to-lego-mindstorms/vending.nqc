// vending.nqc
//  program for the Vending Machine using card reader

#define CARD_COUNT	SENSOR_1
#define CARD_STOP	SENSOR_2
#define CARD_GRAB	OUT_A

#define CANDY_COUNT	SENSOR_3
#define CANDY_MOTOR	OUT_C

#define THRESHOLD 40

int count;

task main()
{
	// setup sensors
	SetSensor(CARD_STOP, SENSOR_LIGHT);
	SetSensor(CARD_COUNT, SENSOR_TOUCH);
	SetSensor(CANDY_COUNT, SENSOR_TOUCH);
	
	while(true)
	{
		readCard();
		dispenseCandy(count);
		ejectCard();
	}
}

void readCard()
{
	// wait for card to be inserted, then grab it
	until(CARD_COUNT == 1);
	OnFwd(CARD_GRAB);

	// start the card counting task
	start countTask;
	
	// continue grabbing until completely inserted
	until(CARD_STOP > THRESHOLD);
	Off(CARD_GRAB);
	
	// stop the counting task
	stop countTask;
}


task countTask()
{
	count = 0;
	
	while(true)
	{
		until(CARD_COUNT == 1);
		count++;
		until(CARD_COUNT == 0);
	}
}


void dispenseCandy(const int &n)
{
	// dispsense the candy
	On(CANDY_MOTOR);
	repeat (n)
	{
		until(CANDY_COUNT == 1);
		until(CANDY_COUNT == 0);
	}
	Off(CANDY_MOTOR);
}


void ejectCard()
{
	// eject card
	OnRev(CARD_GRAB);
	Wait(100);
	until(CARD_COUNT == 0);
	Off(CARD_GRAB);
}
